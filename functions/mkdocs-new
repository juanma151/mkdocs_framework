# vim: filetype=zsh: tabstop=3: shiftwidth=3: noexpandtab:

mkpr-init__ || return 100

local    prjname prjpth initscript baseenvrc prjenvrc ignore
local -r ptn1='/##'
local -r ptn2='*/*'

prjname="$( trim-nnull__ "${1:-}" )" ||
	err__ "The name for the new project cannot be empty"

prjname=${prjname##${~ptn1}}

[[ "${prjname}" == ${~ptn2} ]] &&
	err__ \
		"The name for the project cannot contains '/':" \
		${(qq)prjname}

prjpth=$( path__ base "${prjname}" )

[[ -a "${prjpth}" ]] &&
	err__ "The project already exists in ${(qq)${(D)prjpth}}"

mkdir "${prjpth}"

(
	cd "${prjpth}"
	mkdocs-shell --command bash -c 'mkdocs-init src'
) ||
		err__ \
			"Couldn't initialize mkdocs project in ${(qq)${(D)prjpth}}"


mkdir -p "${prjpth}"/.data/paths
ln -s ../../src      "${prjpth}"/.data/paths/mkdocs-prj
ln -s ../../src/docs "${prjpth}"/.data/paths/docs-src


# envrc for the project (will call the generic envrc)
baseenvrc="$( path__ envrc )"
prjenvrc="${prjpth}"/.envrc

cat - <<eosrc > "${prjenvrc}"
# export the project path
export __MKDOCSPRJS_PRJ_PATH="${prjpth}"
# general .envrc
source_env_if_exists "${baseenvrc}"
eosrc

direnv allow "${prjenvrc}"


## gitignore (just in case)
ignore="${prjpth}"/.gitignore

cat - <<eosgit > "${ignore}"
/site/
/*.pdf
/.envrc
eosgit
